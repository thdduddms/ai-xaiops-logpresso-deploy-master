파라미터 : [string old_system_id, string new_system_id, string new_imx_jdbc_profile, string new_max_jdbc_profile, string new_max_schema, bool save_batchfile]
설명 : 관리자용] 기존 system_id의 인터맥스, 맥스게이지 jdbc수집기를 입력한 정보로 복제하기 | 기존 프로시저를 사용하여 수집설정 생성

# STEP 1: logapi_logger_create 배치 파일 생성
| confdb docs araqne-log-api logger 
| fields configs, factory_name, factory_namespace, name, description
|# logger의 정보 추출
| parsemap overlay=t field=configs | fields - configs
| eval 
		name = split(name, "."), 
        len = len(name),
        is_mxg = if(len == 3, true, false),
        key = case(is_mxg, valueof(name, 2), valueof(name, 1))
 |# 잘못 생성된 logger 제외 (key == table_name)
| search isnotnull(key) and isnotnull(sql)
|# new_max_jdbc_profile (optional). 최소 1개 필수
| search (jdbc_profile == $("new_imx_jdbc_profile") or jdbc_profile == $("new_max_jdbc_profile"))
			and valueof(name, 0) == $("old_system_id") 

|# sql문 내부의 기존 schema, sys_id 변경
| eval         
		schema = if(is_mxg,lower(valueof(name,1)), null),
        sql = if( isnotnull(schema), replace(lower(sql), concat( schema, "."), concat(lower($("new_max_schema")),".")), sql),
        where = if( isnotnull(schema), replace(lower(where), concat( schema, "."), concat(lower($("new_max_schema")),".")), where),
        old_sys_id = valueof(name, 0),
        sql = replace(sql, concat(old_sys_id, " as system_id"), concat($("new_system_id"), " as system_id"))
|# 새로 생성할 logger의 name 변경
| eval 
		name2 = if(is_mxg, concat($("new_max_schema"), ".", valueof(name,2)), valueof(name,1)),
 		name = array( $("new_system_id"), ".", name2),
        name = strjoin("", name)
| stats dc(name) as count, values(jdbc_profile) as jdbc_profile, values(sql) as sql, values(where) as where
by factory_name, factory_namespace, name, column, database_name, date_column, date_format, iter_limit,
key, macro_start_date
| eval sql = valueof(sql,0), where = valueof(where,0), jdbc_profile = valueof(jdbc_profile,0)
|# 배치 생성
| eval 
		batch = concat("logapi.createLogger", " ", factory_name, " ", factory_namespace, " ", name, " ", description, "  <<EOF\n" ),
		batch =concat(batch, jdbc_profile, "\n", sql, "\n", where, "\n", column, "\n", column2, "\n", "\n", "\n", "\n", date_column, "\n" , date_format, "\n"),
        batch = concat(batch, database_name, "\n", macro_date, "\n", iter_limit, "\n", "\n\nEOF\n")
| fields batch
| stats values(batch) as batch
| eval batch = strjoin("", batch) | fields batch

| # STEP 2 : logpresso_logger_create 배치 파일 생성
| union [
confdb docs logpresso loggers
| rename full_name as fullname
| join fullname [
confdb docs araqne-log-api logger ]
| eval
		jdbc_profile = valueof(configs, "new_imx_jdbc_profile"),
		name = split(name, "."),
		is_mxg = if(len(name) == 3 , true, false),
		system_id = valueof(name, 0), 
        schema = if(is_mxg, valueof(name, 1), null)
| search system_id == $("old_system_id") and ( jdbc_profile == $("new_imx_jdbc_profile") or jdbc_profile == $("new_max_jdbc_profile"))
| fields fullname, table_name, host, schema, system_id
| eval 
		fullname = replace( fullname, schema, $("new_max_schema")),
        fullname = replace( fullname, system_id, $("new_system_id")),
        host = replace( host, schema, $("new_max_schema")),
        host = replace( host, system_id, $("new_system_id"))
| eval 
    batch = concat( "logpresso.createLogger " , fullname, " ", table_name, " ", host, " <<EOF\nEOF\n")
| stats values(batch) as batch | eval batch = strjoin("", batch)  ]
| stats values(batch) as batch
| eval batch = strjoin("\n", batch)

| search $("save_batchfile")
|# STEP 3 : BATCH 파일 생성
| outputtxt overwrite=t partition=t encoding="UTF-8" script/job/{now:yyyyMMdd}/create_logger_local_jdbc_copy.batch batch

|# STEP 4: 프로시저 실행 후 아라크네 엔진 > 
| json "{}" 
| eval cli_cmd = concat( "batch.executeFile script/job/",str(now(),"yyyyMMdd"), "/create_logger_local_jdbc_copy.batch")